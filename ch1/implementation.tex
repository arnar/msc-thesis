\section{Implementation}
\label{sec:implementation}

Our Haskell implementation is comprised of one module, \verb+TMI+. 
The most important components are the
monad \lstinline+TMI d a+ and the type for TMI variables, \lstinline+TMIVar d a+. 
Both are parameterized on the descriptor type \lstinline+d+, which is chosen by the user
of this module. A TMI variable is represented by a descriptor value and an STM \lstinline+TVar+,
\begin{lstlisting}[style=small]
data TMIVar d a = TMIVar {
      getTVar       :: TVar a,
      getDescriptor :: d
}
\end{lstlisting}
The field accessors \lstinline+getTVar+ and \lstinline+getDescriptor+ are not exported and
only available inside the \lstinline+TMI+ module.

The TMI monad is a stack of the standard {\em writer monad} on top of the regular STM
monad. The writer monad has the ability of collecting accumulating information in a
sequence, which is exactly what we need to maintain the introspection log.
In the TMI module, the regular STM module is imported under the name \lstinline+T+.
\begin{lstlisting}[style=small]
newtype TMI d a = TM {
   unwrapTM :: WriterT (TMILog d) T.STM a
} deriving (Monad)
\end{lstlisting}
The type \lstinline+TMILog d+ represents a log of all accesses to TMI variables.
It is defined by the following declarations.
\begin{lstlisting}[style=small]
data TMIAccess = CreateVar | ReadVar | WriteVar
type TMILog d = [(TMIAccess, d)]
\end{lstlisting}
For inserting entries in the log, we define the following shortcut, where \lstinline+tell+
is the standard function that the writer monad uses to collect information.
\begin{lstlisting}[style=small]
log :: (TMIAccess, d) -> STM d ()
log entry = (TM . tell) [entry]
\end{lstlisting}
\lstinline+log+ returns an STM action which has the
only effect of appending its argument to the introspection log. Note that this
helper is not exported, so users of the TMI module cannot append to the log
directly.
As expected, a log entry of the form \lstinline+(ReadVar, x)+ just means that a variable with descriptor
value \lstinline+x+ was read. 

The \lstinline+liftSTM+ function lifts an STM operation to a TMI operation. The log
is not affected by the work performed in the STM action.
\begin{lstlisting}[style=small]
liftSTM :: STM a -> TMI d a
liftSTM = TM . lift
\end{lstlisting}

The functions to create, read and write TMI variables are now simple to define. They
all enter the relevant entries to the log and then call the underlying functions
from the STM module.
\begin{lstlisting}[style=small]
newTMIVar :: d -> a -> TMI d (TMIVar d a)
newTMIVar description val =
    do log (CreateVar, description)
       var <- liftSTM (T.newTVar val)
       return (TMIVar var description)

readTMIVar :: TMIVar d a -> TMI d a
readTMIVar tv = 
    do log (ReadVar, getDescriptor tv)
       liftSTM (T.readTVar (getTVar tv))

writeTMIVar :: TMIVar d a -> a -> TMI d ()
writeTMIVar tv val = 
    do log (WriteVar, getDescriptor tv)
       liftSTM (T.writeTVar (getTVar tv) val)
\end{lstlisting}

The combinators from the STM world are defined thus.
\begin{lstlisting}[style=small]
retryTMI = liftSTM T.retry

runTMI = runWriterT . unwrapTM   -- helper

orElseTMI t1 t2 = TM . WriterT (runTMI t1 `T.orElse` runTMI t2)
\end{lstlisting}

What is left is to define the crucial \lstinline+authorized+ function. This function
accepts an authorization function with the type \lstinline+TMILog d -> Bool+
and a TMI action; it should return an STM action that performs the operation
of the TMI action, validates the resulting log with the authorization function and
either returns the result or throws an exception. With this description in mind,
the implementation is pretty straight-forward.
\begin{lstlisting}[style=small]
authorized :: (TMILog d -> Bool) -> TMI d a -> T.STM a
authorized auth act =  do
   (result, log) <- runTMI act
   if not (auth log)
      then throw (AssertionFailed "Access denied")
      else return result
\end{lstlisting}
Note that a custom exception may be more appropriate but for sake of clarity
we simply use the standard assertion failure to trigger a transaction abort.

Our Haskell TMI implementation can support variants of
history-based policy enforcement~\cite{HBAC},
in particular
allowing \emph{privilege amplification} with nested TMI actions as described in
the previous section.
%
In particular a call to \lstinline+authorized+ will return
an STM action which {\em contains} a TMI operation and an associated
authorization closure. When
code inside a TMI action needs increased privileges it can nest a call to \lstinline+authorized+
with a different authorization manager and use \lstinline+liftSTM+ to lift the resulting
STM operation back to the TMI level.

As in stack inspection~\cite{GordonFournetPOPL},
privilege amplification provides 
TMI security managers with
a useful 
escape hatch to perform operations as a more powerful ``application principal''.
%
% For instance, we have used it 
% in cases where applications need to compute aggregates, 
% such as in an grade-management application
% that shows a student their grade and a class grade average,
% but prevents access to other student's grades.

